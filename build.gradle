buildscript {
  repositories {
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }
  dependencies {
    classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3"
  }
}


apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'eclipse'
apply plugin: 'idea'


defaultTasks 'clean', 'build', 'publish'
group = 'coyote'
version = '0.0.3'

ext {
	title = 'Coyote Web Server'
	provider = 'Coyote Systems'
}

jar {
    manifest {
		attributes  'Implementation-Title' : "$title",
		'Provider' : "$provider",
		'Main-Class' : 'coyote.loader.BootStrap',
		'Implementation-Version' : version
	}
	from {
		configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } 
	}{
		exclude "META-INF/*.RSA"
	}
}

repositories {
	maven { url "https://dl.bintray.com/sdcote/maven" }
	mavenLocal()
	mavenCentral()
}


ext {
	loaderVersion = '0.7.2'
	junitVersion = '4.12'
}
dependencies {
	compile "coyote:Loader:$loaderVersion"
	testCompile "junit:junit:$junitVersion"
}



task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}


task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from javadoc.destinationDir
}


artifacts {
	archives sourcesJar, javadocJar
}


publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java

			artifact sourcesJar {
				classifier "sources"
			}

			artifact javadocJar {
				classifier "javadoc"
			}
		}
	}
	
	repositories {
		mavenLocal()
	}
}


task wrapper(type: Wrapper) {
	gradleVersion = '4.2'
}


bintray {
	user = bintrayUser
	key = bintrayKey
	publications = ['mavenJava']
	pkg {
		repo = 'maven'
		name = 'CoyoteWeb'
		desc = 'Small, embeddable web server using the Coyote Loader toolkit.'
		licenses = ['MIT']
		labels = ['Java', 'HTTPD','WebServer']
		vcsUrl = 'https://github.com/sdcote/coyoteweb.git'
	}
}

