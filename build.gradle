apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'eclipse'
apply plugin: 'idea'


defaultTasks 'clean', 'build', 'publish'
group = 'coyote'
version = '0.1'

ext {
	title = 'Coyote Web Server'
	provider = 'Coyote Systems'
}

jar {
    manifest {
		attributes  'Implementation-Title' : "$title",
		'Provider' : "$provider",
		'Main-Class' : 'coyote.loader.BootStrap',
		'Implementation-Version' : version
	}
	from {
		configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } 
	}{
		exclude "META-INF/*.RSA"
	}
}

repositories {
	mavenLocal()
	mavenCentral()
}


ext {
	loaderVersion = '1.1'
	junitVersion = '4.11'
}
dependencies {
	compile "coyote:Loader:$loaderVersion"
	testCompile "junit:junit:$junitVersion"
}



// custom tasks for creating source/javadoc jars
task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}


task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from javadoc.destinationDir
}


// add javadoc/source jar tasks as artifacts
artifacts {
	archives sourcesJar, javadocJar
}


publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java

			artifact sourcesJar {
				classifier "sources"
			}

			artifact javadocJar {
				classifier "javadoc"
			}
		}
	}
	
	repositories {
		mavenLocal()
	}
}


task wrapper(type: Wrapper) {
	gradleVersion = '2.14'
}
